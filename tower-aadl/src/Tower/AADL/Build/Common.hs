{-# LANGUAGE Rank2Types #-}
-- Create Ramses build script.
--
-- (c) 2015 Galois, Inc.
--

module Tower.AADL.Build.Common where

import Data.Char
import Data.Maybe (maybeToList, fromMaybe)
import System.FilePath
import Text.PrettyPrint.Leijen hiding ((</>))

import Ivory.Artifact
import Ivory.Tower

import qualified Ivory.Compile.C.CmdlineFrontend as O

import Tower.AADL.Config (AADLConfig(..))
import Tower.AADL.Compile

data Required
  = Req
  | Opt
  deriving (Read, Show, Eq)

data Assign
  = Equals
  | ColonEq
  | QuestionEq
  | PlusEq
  deriving (Read, Show, Eq)

data Export
  = NoExport
  | Export
  deriving (Read, Show, Eq)

data MkStmt
  = Include Required FilePath
  | Var Export String Assign String
  | Target String [String] [String]
  | IfNDef String [MkStmt] [MkStmt]
  | Comment String
  deriving (Read, Show, Eq)


-- Combinators to make building make statements easier ------------------------
include :: FilePath -> MkStmt
include fname = Include Req fname

includeOpt :: FilePath -> MkStmt
includeOpt fname = Include Opt fname

infixr 4 ?=, =:, +=, ===

(?=) :: String -> String -> MkStmt
var ?= val = Var NoExport var QuestionEq val

(=:) :: String -> String -> MkStmt
var =: val = Var NoExport var ColonEq val

(+=) :: String -> String -> MkStmt
var += val = Var NoExport var PlusEq val

(===) :: String -> String -> MkStmt
var === val = Var NoExport var Equals val

export :: MkStmt -> MkStmt
export (Var _ var assign val) = Var Export var assign val
export s                      = s
-------------------------------------------------------------------------------

-- Makefile pretty printer ----------------------------------------------------
renderExport :: Export -> Doc
renderExport NoExport = empty
renderExport Export   = text "export "

renderAssign :: Assign -> Doc
renderAssign Equals     = char '='
renderAssign ColonEq    = text " := "
renderAssign QuestionEq = text " ?= "
renderAssign PlusEq     = text " += "

renderMkStmt :: MkStmt -> Doc
renderMkStmt (Include Req fp) = text "include"  <+> text fp
renderMkStmt (Include Opt fp) = text "-include" <+> text fp
renderMkStmt (Var expt var assign val)  =
  renderExport expt <> text var <> renderAssign assign <> text val
renderMkStmt (Target name deps actions) =
     text name <> text ":" <+> hsep (map text deps)
  <> foldr (\str acc -> linebreak <> char '\t' <> text str <> acc) empty actions
  <> linebreak
renderMkStmt (IfNDef var t e) =
       text "ifndef" <+> text var
  <$$> vsep (map renderMkStmt t)
  <$$> text "else"
  <$$> vsep (map renderMkStmt e)
  <$$> text "endif"
renderMkStmt (Comment msg) = char '#' <+> text msg

renderMkStmts :: [MkStmt] -> String
renderMkStmts stmts = show $
  foldr (\mkstmt acc -> renderMkStmt mkstmt <> linebreak <> linebreak <> acc)
        empty
        (autogenComment : stmts)
  where
  autogenComment = Comment "This makefile is autogenerated. DO NOT EDIT."
-------------------------------------------------------------------------------

ramsesMakefileName :: String
ramsesMakefileName = "ramses.mk"

aadlFilesMk :: String
aadlFilesMk = "AADL_FILES.mk"

componentLibsName :: String
componentLibsName = "componentlibs.mk"

mkLib :: AADLConfig -> [String] -> String
mkLib c aadlFileNames =
  unlines (map go aadlFileNames) ++ []
  where
  go m = m ++ "_LIBS += " ++ configLibDir c

makefileName :: String
makefileName = "Makefile"

aadlDocNames :: CompiledDocs -> [String]
aadlDocNames docs = map docName $
  maybeToList (tyDoc docs) ++ thdDocs docs
--------------------------------------------------------------------------------
-- Helpers

shellVar :: String -> String
shellVar = map toUpper

--------------------------------------------------------------------------------
-- Support for OS Specific Code generation
--------------------------------------------------------------------------------

data OSSpecific a = OSSpecific
  { osSpecificName      :: String
  , osSpecificConfig    :: a
  , osSpecificArtifacts :: String -> AADLConfig -> [String] -> [Located Artifact]
  , osSpecificSrcDir    :: AADLConfig -> Located Artifact -> Located Artifact
  , osSpecificTower     :: forall e. Tower e ()
  , osSpecificOptsApps  :: AADLConfig -> O.Opts -> O.Opts
  , osSpecificOptsLibs  :: AADLConfig -> O.Opts -> O.Opts
  }

defaultOptsUpdate :: AADLConfig -> O.Opts -> O.Opts
defaultOptsUpdate c copts =
  copts { O.outDir    = Just (dir </> configSrcsDir c)
        , O.outHdrDir = Just (dir </> configHdrDir  c)
        , O.outArtDir = Just dir
        }
  where
  dir = fromMaybe "." (O.outDir copts)
